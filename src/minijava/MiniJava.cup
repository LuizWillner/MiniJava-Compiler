package minijava;
import java_cup.runtime.*;
import minijava.ASTNode;

parser code {:
    private boolean syntaxErrors = false;
    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Erro de Sintaxe");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {
                m.append(" na linha "+(s.left+1));
                if (s.right >= 0)
                    m.append(", coluna "+(s.right+1));
            }
        }
        m.append(" : "+message);
        System.out.println(m);
        this.syntaxErrors = true;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }

    public boolean hasErrors() {
        return this.syntaxErrors;
    }
:};

/* Terminais */
terminal CLASS, PUBLIC, STATIC, VOID, MAIN, EXTENDS, RETURN;
terminal INT, BOOLEAN, STRING_TYPE;
terminal IF, ELSE, WHILE;
terminal TRUE, FALSE, THIS, NEW, LENGTH, PRINT;
terminal AND, LESSTHAN, PLUS, MINUS, MULT, NOT;
terminal ASSIGN, SEMICOL, COMMA, DOT;
terminal LBRACK, RBRACK, LPAREN, RPAREN, LBRACE, RBRACE;
terminal String ID, NUMBER;
terminal ERROR;

/* Não-terminais */
/* Não-terminais */
non terminal ASTNode Goal, MainClass, ClassDeclaration;
non terminal ASTNode VarDeclaration, MethodDeclaration;
non terminal ASTNode Type, Statement, Expression;
non terminal ASTNode ClassDeclarationList, VarDeclarationList, MethodDeclarationList;
non terminal ASTNode StatementList, ExpressionList, ParameterList, Parameter;

/* Precedência */
precedence left AND;
precedence left LESSTHAN;
precedence left PLUS, MINUS;
precedence left MULT;
precedence right NOT;
precedence left DOT, LBRACK;

/* Gramática */
start with Goal;

Goal ::=
    MainClass:mc ClassDeclarationList:cdl
    {:
        ASTNode goal = new ASTNode("Goal");
        goal.addChild(mc);
        if (cdl != null) goal.addChild(cdl);
        RESULT = goal;
    :}
    | MainClass:mc
    {:
        ASTNode goal = new ASTNode("Goal");
        goal.addChild(mc);
        RESULT = goal;
    :}
    ;

ClassDeclarationList ::=
    ClassDeclaration:cd
    {:
        ASTNode list = new ASTNode("ClassDeclarationList");
        list.addChild(cd);
        RESULT = list;
    :}
    | ClassDeclarationList:cdl ClassDeclaration:cd
    {:
        cdl.addChild(cd);
        RESULT = cdl;
    :}
    ;

MainClass ::=
    CLASS ID:name LBRACE PUBLIC STATIC VOID MAIN LPAREN STRING_TYPE LBRACK RBRACK ID:args RPAREN LBRACE Statement:stmt RBRACE RBRACE
    {:
        ASTNode mainClass = new ASTNode("MainClass", name);
        ASTNode argsNode = new ASTNode("Args", args);
        mainClass.addChild(argsNode);
        mainClass.addChild(stmt);
        RESULT = mainClass;
    :}
    ;

ClassDeclaration ::=
    CLASS ID:name LBRACE VarDeclarationList:vdl MethodDeclarationList:mdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        if (vdl != null) classDecl.addChild(vdl);
        if (mdl != null) classDecl.addChild(mdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name LBRACE VarDeclarationList:vdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        if (vdl != null) classDecl.addChild(vdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name LBRACE MethodDeclarationList:mdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        if (mdl != null) classDecl.addChild(mdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name LBRACE RBRACE
    {:
        RESULT = new ASTNode("ClassDeclaration", name);
    :}
    | CLASS ID:name EXTENDS ID:parent LBRACE VarDeclarationList:vdl MethodDeclarationList:mdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        ASTNode extendsNode = new ASTNode("Extends", parent);
        classDecl.addChild(extendsNode);
        if (vdl != null) classDecl.addChild(vdl);
        if (mdl != null) classDecl.addChild(mdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name EXTENDS ID:parent LBRACE VarDeclarationList:vdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        ASTNode extendsNode = new ASTNode("Extends", parent);
        classDecl.addChild(extendsNode);
        if (vdl != null) classDecl.addChild(vdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name EXTENDS ID:parent LBRACE MethodDeclarationList:mdl RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        ASTNode extendsNode = new ASTNode("Extends", parent);
        classDecl.addChild(extendsNode);
        if (mdl != null) classDecl.addChild(mdl);
        RESULT = classDecl;
    :}
    | CLASS ID:name EXTENDS ID:parent LBRACE RBRACE
    {:
        ASTNode classDecl = new ASTNode("ClassDeclaration", name);
        ASTNode extendsNode = new ASTNode("Extends", parent);
        classDecl.addChild(extendsNode);
        RESULT = classDecl;
    :}
    ;

VarDeclarationList ::=
    VarDeclaration:vd
    {:
        ASTNode list = new ASTNode("VarDeclarationList");
        list.addChild(vd);
        RESULT = list;
    :}
    | VarDeclarationList:vdl VarDeclaration:vd
    {:
        vdl.addChild(vd);
        RESULT = vdl;
    :}
    ;

VarDeclaration ::=
    Type:t ID:name SEMICOL
    {:
        ASTNode varDecl = new ASTNode("VarDeclaration", name);
        varDecl.addChild(t);
        RESULT = varDecl;
    :}
    ;

MethodDeclarationList ::=
    MethodDeclaration:md
    {:
        ASTNode list = new ASTNode("MethodDeclarationList");
        list.addChild(md);
        RESULT = list;
    :}
    | MethodDeclarationList:mdl MethodDeclaration:md
    {:
        mdl.addChild(md);
        RESULT = mdl;
    :}
    ;

MethodDeclaration ::=
    PUBLIC Type:t ID:name LPAREN ParameterList:pl RPAREN LBRACE VarDeclarationList:vdl StatementList:sl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (pl != null) method.addChild(pl);
        if (vdl != null) method.addChild(vdl);
        if (sl != null) method.addChild(sl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN RPAREN LBRACE VarDeclarationList:vdl StatementList:sl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (vdl != null) method.addChild(vdl);
        if (sl != null) method.addChild(sl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN ParameterList:pl RPAREN LBRACE StatementList:sl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (pl != null) method.addChild(pl);
        if (sl != null) method.addChild(sl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN RPAREN LBRACE StatementList:sl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (sl != null) method.addChild(sl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN ParameterList:pl RPAREN LBRACE VarDeclarationList:vdl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (pl != null) method.addChild(pl);
        if (vdl != null) method.addChild(vdl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN RPAREN LBRACE VarDeclarationList:vdl RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (vdl != null) method.addChild(vdl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN ParameterList:pl RPAREN LBRACE RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        if (pl != null) method.addChild(pl);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    | PUBLIC Type:t ID:name LPAREN RPAREN LBRACE RETURN Expression:e SEMICOL RBRACE
    {:
        ASTNode method = new ASTNode("MethodDeclaration", name);
        method.addChild(t);
        ASTNode returnNode = new ASTNode("Return");
        returnNode.addChild(e);
        method.addChild(returnNode);
        RESULT = method;
    :}
    ;

ParameterList ::=
    Parameter:p
    {:
        ASTNode list = new ASTNode("ParameterList");
        list.addChild(p);
        RESULT = list;
    :}
    | ParameterList:pl COMMA Parameter:p
    {:
        pl.addChild(p);
        RESULT = pl;
    :}
    ;

Parameter ::=
    Type:t ID:name
    {:
        ASTNode param = new ASTNode("Parameter", name);
        param.addChild(t);
        RESULT = param;
    :}
    ;

Type ::=
    INT LBRACK RBRACK
    {: RESULT = new ASTNode("Type", "int[]"); :}
    | BOOLEAN
    {: RESULT = new ASTNode("Type", "boolean"); :}
    | INT
    {: RESULT = new ASTNode("Type", "int"); :}
    | ID:name
    {: RESULT = new ASTNode("Type", name); :}
    ;

StatementList ::=
    Statement:s
    {:
        ASTNode list = new ASTNode("StatementList");
        list.addChild(s);
        RESULT = list;
    :}
    | StatementList:sl Statement:s
    {:
        sl.addChild(s);
        RESULT = sl;
    :}
    ;

Statement ::=
    LBRACE StatementList:sl RBRACE
    {:
        ASTNode block = new ASTNode("Block");
        if (sl != null) block.addChild(sl);
        RESULT = block;
    :}
    | LBRACE RBRACE
    {: RESULT = new ASTNode("Block"); :}
    | IF LPAREN Expression:e RPAREN Statement:s1 ELSE Statement:s2
    {:
        ASTNode ifStmt = new ASTNode("If");
        ifStmt.addChild(e);
        ifStmt.addChild(s1);
        ASTNode elseStmt = new ASTNode("Else");
        elseStmt.addChild(s2);
        ifStmt.addChild(elseStmt);
        RESULT = ifStmt;
    :}
    | WHILE LPAREN Expression:e RPAREN Statement:s
    {:
        ASTNode whileStmt = new ASTNode("While");
        whileStmt.addChild(e);
        whileStmt.addChild(s);
        RESULT = whileStmt;
    :}
    | PRINT LPAREN Expression:e RPAREN SEMICOL
    {:
        ASTNode printStmt = new ASTNode("Print");
        printStmt.addChild(e);
        RESULT = printStmt;
    :}
    | ID:name ASSIGN Expression:e SEMICOL
    {:
        ASTNode assign = new ASTNode("Assign", name);
        assign.addChild(e);
        RESULT = assign;
    :}
    | ID:name LBRACK Expression:e1 RBRACK ASSIGN Expression:e2 SEMICOL
    {:
        ASTNode arrayAssign = new ASTNode("ArrayAssign", name);
        arrayAssign.addChild(e1);
        arrayAssign.addChild(e2);
        RESULT = arrayAssign;
    :}
    ;

Expression ::=
    Expression:e1 AND Expression:e2
    {:
        ASTNode and = new ASTNode("And");
        and.addChild(e1);
        and.addChild(e2);
        RESULT = and;
    :}
    | Expression:e1 LESSTHAN Expression:e2
    {:
        ASTNode lessThan = new ASTNode("LessThan");
        lessThan.addChild(e1);
        lessThan.addChild(e2);
        RESULT = lessThan;
    :}
    | Expression:e1 PLUS Expression:e2
    {:
        ASTNode plus = new ASTNode("Plus");
        plus.addChild(e1);
        plus.addChild(e2);
        RESULT = plus;
    :}
    | Expression:e1 MINUS Expression:e2
    {:
        ASTNode minus = new ASTNode("Minus");
        minus.addChild(e1);
        minus.addChild(e2);
        RESULT = minus;
    :}
    | Expression:e1 MULT Expression:e2
    {:
        ASTNode mult = new ASTNode("Mult");
        mult.addChild(e1);
        mult.addChild(e2);
        RESULT = mult;
    :}
    | Expression:e1 LBRACK Expression:e2 RBRACK
    {:
        ASTNode arrayAccess = new ASTNode("ArrayAccess");
        arrayAccess.addChild(e1);
        arrayAccess.addChild(e2);
        RESULT = arrayAccess;
    :}
    | Expression:e DOT LENGTH
    {:
        ASTNode length = new ASTNode("Length");
        length.addChild(e);
        RESULT = length;
    :}
    | Expression:e DOT ID:method LPAREN ExpressionList:el RPAREN
    {:
        ASTNode call = new ASTNode("MethodCall", method);
        call.addChild(e);
        if (el != null) call.addChild(el);
        RESULT = call;
    :}
    | Expression:e DOT ID:method LPAREN RPAREN
    {:
        ASTNode call = new ASTNode("MethodCall", method);
        call.addChild(e);
        RESULT = call;
    :}
    | NUMBER:n
    {: RESULT = new ASTNode("Number", n); :}
    | TRUE
    {: RESULT = new ASTNode("Boolean", "true"); :}
    | FALSE
    {: RESULT = new ASTNode("Boolean", "false"); :}
    | ID:name
    {: RESULT = new ASTNode("Identifier", name); :}
    | THIS
    {: RESULT = new ASTNode("This"); :}
    | NEW INT LBRACK Expression:e RBRACK
    {:
        ASTNode newArray = new ASTNode("NewArray");
        newArray.addChild(e);
        RESULT = newArray;
    :}
    | NEW ID:name LPAREN RPAREN
    {: RESULT = new ASTNode("NewObject", name); :}
    | NOT Expression:e
    {:
        ASTNode not = new ASTNode("Not");
        not.addChild(e);
        RESULT = not;
    :}
    | LPAREN Expression:e RPAREN
    {: RESULT = e; :}
    ;

ExpressionList ::=
    Expression:e
    {:
        ASTNode list = new ASTNode("ExpressionList");
        list.addChild(e);
        RESULT = list;
    :}
    | ExpressionList:el COMMA Expression:e
    {:
        el.addChild(e);
        RESULT = el;
    :}
    ;